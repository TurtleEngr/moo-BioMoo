$Header: /repo/public.cvs/app/BioGate/BioMooCore/core0.moo,v 1.1 2021/07/27 06:44:25 bruce Exp $

>@dump #0 with create
@create $root_class named The System Object:The System Object
@prop #0."builder" #4 rc
@prop #0."login" #10 r
@prop #0."last_huh" #11 r
@prop #0."guest_log" #12 r
@prop #0."last_restart_time" 924537073 rc
@prop #0."biglist" #13 rc
@prop #0."big_mail_recipient" #14 rc
@prop #0."limbo" #15 rc
@prop #0."registration_db" #16 rc
@prop #0."new_player_log" #17 rc
@prop #0."verb_help" #18 rc
@prop #0."core_help" #19 rc
@prop #0."prog_help" #22 rc
@prop #0."wiz_help" #23 rc
@prop #0."shutdown_task" E_NONE rc
@prop #0."wiz_utils" #24 rc
@prop #0."site_db" #25 rc
@prop #0."math_utils" #26 rc
@prop #0."set_utils" #27 rc
@prop #0."builtin_function_help" #28 rc
@prop #0."new_prog_log" #29 rc
@prop #0."generic_help" #30 rc
@prop #0."guest" #31 r
@prop #0."spell" #32 rc
@prop #0."seq_utils" #33 rc
@prop #0."quota_log" #34 rc
@prop #0."you" #35 r #36
@prop #0."max_seconds" 5 rc
@prop #0."max_ticks" 30000 rc
@prop #0."hacker" #36 rc
@prop #0."generic_db" #37 rc
@prop #0."shutdown_message" "Amalgam (#177): " rc
@prop #0."shutdown_time" 890209374 rc
@prop #0."no_one" #38 r
@prop #0."player_db" #39 r
@prop #0."class_registry" {} rc
;;#0.("class_registry") = {{"generics", "Generic objects intended for use as the parents of new objects", {#3, #7, #5, #9, #54, #8, #1, #6, #58, #57, #50, #45, #46}}, {"utilities", "Objects holding useful general-purpose verbs", {#20, #55, #24, #27, #41, #26, #43, #51, #52, #53, #56, #42, #21, #33, #13, #80, #82, #59, #93}}}
@prop #0."player_class" #4 rc
@prop #0."gender_utils" #41 r
@prop #0."trig_utils" #26 rc
@prop #0."time_utils" #43 rc
@prop #0."editor_help" #44 rc
@prop #0."mail_recipient" #45 rc
@prop #0."mail_agent" #46 rc
@prop #0."mail_editor" #47 rc
@prop #0."note_editor" #48 rc
@prop #0."verb_editor" #49 rc
@prop #0."generic_editor" #50 rc
@prop #0."match_utils" #51 rc
@prop #0."object_utils" #52 rc
@prop #0."lock_utils" #53 rc
@prop #0."gripe_recipients" {} rc
;;#0.("gripe_recipients") = {#2}
@prop #0."letter" #54 rc
@prop #0."dump_interval" 10800 rc
@prop #0."list_utils" #55 rc
@prop #0."command_utils" #280 rc
@prop #0."player" #6 rc
@prop #0."wiz" #57 rc
@prop #0."prog" #58 rc
@prop #0."code_utils" #59 rc
@prop #0."help" #60 rc
@prop #0."nothing" #-1 rc
@prop #0."failed_match" #-3 rc
@prop #0."ambiguous_match" #-2 rc
@prop #0."perm_utils" #42 rc
@prop #0."building_utils" #21 rc
@prop #0."string_utils" #20 rc
@prop #0."news" #61 rc
@prop #0."note" #9 rc
@prop #0."container" #8 rc
@prop #0."thing" #5 rc
@prop #0."exit" #7 rc
@prop #0."room" #3 rc
@prop #0."player_start" #341 rc
@prop #0."root_class" #1 rc
@prop #0."recycler" #63 rc
@prop #0."garbage" #64 rc
@prop #0."mail_options" #65 rc
@prop #0."edit_options" #66 rc
@prop #0."display_options" #67 rc
@prop #0."generic_options" #68 rc
@prop #0."maxint" 2147483647 rc
@prop #0."minint" -2147483648 rc
@prop #0."error" #69 rc
@prop #0."newt_log" #70 rc
@prop #0."toad_log" #70 rc
@prop #0."site_log" #70 rc
@prop #0."housekeeper" #71 rc
@prop #0."network" #72 rc
@prop #0."generic_biglist_home" #73 r
@prop #0."feature" #74 rc
@prop #0."local" #290 rc
@prop #0."gopher" #75 r
@prop #0."prog_options" #76 rc
@prop #0."build_options" #77 rc
@prop #0."mail_name_db" #78 rc
@prop #0."generic_utils" #79 rc
@prop #0."quota_utils" #80 rc
@prop #0."paranoid_db" #81 r
@prop #0."no_connect_message" 0 rc
@prop #0."sysobj" #0 r
@prop #0."byte_quota_utils" #80 rc
@prop #0."object_quota_utils" #82 rc
@prop #0."server_options" #83 rc
@prop #0."feature_warehouse" #84 r
@prop #0."builder_help" #85 r
@prop #0."mail_help" #86 r
@prop #0."ftp" #87 rc
@prop #0."password_verifier" #88 r
@prop #0."new_password_log" #89 r
@prop #0."frand_class" #90 rc
@prop #0."mail_recipient_class" #40 rc
@prop #0."stage_talk" #91 rc
@prop #0."pasting_feature" #92 rc
@prop #0."core_history" {} r
;;#0.("core_history") = {{"LambdaMOO", "1.8.0p5", 854992537}}
@prop #0."matrix_utils" #93 r #36
@prop #0."webapp" #98 r #95
@prop #0."webviewer_cstm" #99 r #95
@prop #0."web_who" #100 r #95
@prop #0."teleporter" #101 r #95
@prop #0."web_geninfo" #102 r #95
@prop #0."web_options" #104 r #95
@prop #0."web_utils" #105 r #95
@prop #0."http_port" #106 r #95
@prop #0."std_vrml10viewer" #107 r #95
@prop #0."vrml_utils" #108 r #95
@prop #0."jclient_handler" #109 r #95
@prop #0."frame_manager" #111 r #95
@prop #0."mcp" #112 r #95
@prop #0."anon_webviewer" #113 r #95
@prop #0."mail_browser" #114 r #95
@prop #0."http_handler" #115 r #95
@prop #0."webchar_cstm" #116 r #95
@prop #0."standard_webviewer" #117 r #95
@prop #0."object_browser" #118 r #95
@prop #0."html_editor" #119 r #95
@prop #0."orig_interior_room" #120 rc
@prop #0."interior_room" #121 rc
@prop #0."bb" #123 rc
@prop #0."bb_mail_recipient" #124 rc
@prop #0."sign" #125 rc
@prop #0."file_utils" #126 rc
@prop #0."file_handler" #127 rc
@prop #0."handy_thing" #130 rc
@prop #0."magnetic_thing" #131 rc
@prop #0."lag_meter" #132 rc
@prop #0."portable_room" #121 rc
@prop #0."last_tb" {} rc
;;#0.("last_tb") = {938070000, #177, E_PERM, "Permission denied", 0, {{#69, "raise", #36, #69, #177, 1}, {#81, "gc", #36, #81, #177, 2}, {#81, "weekly", #2, #81, #177, 8}, {#81, "weekly", #2, #81, #177, 6}}, {"#69:raise, line 1:  Permission denied", "... called from #81:gc, line 2", "... called from #81:weekly, line 8", "... called from #81:weekly, line 6", "(End of traceback)"}}
@prop #0."command_utils_core" #56 rc
@prop #0."decore_player" #284 rc
@prop #0."goes_player" #284 rc
@prop #0."graph_utils" #447 rc
@prop #0."moograph" #448 rc
@prop #0."printout" #449 rc
@prop #0."vidlog" #450 rc
@prop #0."graph_demo" #451 rc
@prop #0."palette" #452 rc
@prop #0."whoson" #453 rc
@prop #0."slide" #535 rc
@prop #0."studio" #555 rc
@prop #0."masterphones" #558 rc
@prop #0."mastercd" #559 rc
@prop #0."cdplayer" #560 rc
@prop #0."headphone_dispenser" #561 rc
@prop #0."clothing" #563 rc
@prop #0."cdmanual" #568 rc
;;#0.("aliases") = {"The System Object"}
;;#0.("description") = "The known universe."
;;#0.("object_size") = {22624, 1577149621}

@verb #0:"do_login_command" this none this
@program #0:do_login_command
"...This code should only be run as a server task...";
if (callers())
  return E_PERM;
endif
if (typeof(h = $network:incoming_connection(player)) == OBJ)
  "connected to an object";
  return h;
elseif (h)
  return 0;
endif
host = $string_utils:connection_hostname(connection_name(player));
if ($login:redlisted(host))
  boot_player(player);
  server_log(tostr("REDLISTED: ", player, " from ", host));
  return 0;
endif
"...checks to see if the login is spamming the server with too many commands...";
if (!$login:maybe_limit_commands())
  " --- used by MCP-aware telnet clients ---- ";
  if (($mcp:is_reading(player) == 1) && $mcp:process_reading_line(player, argstr))
    return 0;
  endif
  " ---- ";
  args = $login:parse_command(@args);
  return $login:(args[1])(@listdelete(args, 1));
endif
.

@verb #0:"server_started" this none this
@program #0:server_started
if (callers())
  "This code should only be run as a server task.";
  return E_PERM;
endif
$network:server_started();
$login:server_started();
$last_restart_time = time();
$lag_meter:initialize();
$http_handler:server_started();
Main_Auth_HTTP_Port = #128;
Main_Auth_HTTP_Port:server_started();
Main_Cookie_HTTP_Port = #129;
Main_Cookie_HTTP_Port:server_started();
"... suitable prefix for server log messages...";
preamble = "STARTUP: [#0:server_started]  ";
server_log(tostr(preamble, "Checkpoint scheduled every ", $dump_interval / 60, " minutes."));
"...do some basic sanity checks for $maxint and $minint...";
if (((($maxint + 1) != $minint) || ($minint >= 0)) || ($maxint <= 0))
  server_log(preamble + "Warning:  $maxint and $minint look incorrect.");
endif
.

@verb #0:"core_objects" this none this
@program #0:core_objects
saved = {#0};
for p in (properties(#0))
  v = #0.(p);
  if ((typeof(v) == OBJ) && valid(v))
    saved = setadd(saved, v);
  endif
endfor
for o in (saved)
  p = parent(o);
  while (valid(p))
    saved = setadd(saved, p);
    p = parent(p);
  endwhile
endfor
return $list_utils:sort(saved);
.

@verb #0:"init_for_core" this none this
@program #0:init_for_core
if (caller_perms().wizard)
  pass();
  if ("server_started" in verbs(this))
    code = {"callers() || ($last_restart_time = time());"};
    set_verb_code(this, "server_started", code);
  endif
  if ("uptime_since" in verbs(this))
    delete_verb(this, "uptime_since");
  endif
  if ("do_command" in verbs(this))
    delete_verb(this, "do_command");
  endif
  `delete_verb(this, "checkpoint_finished") ! E_VERBNF';
  $core_history = {{$network.MOO_name, server_version(), time()}, @$core_history};
  $shutdown_message = "";
  $shutdown_time = 0;
  $dump_interval = 3600;
  $gripe_recipients = {player};
  $class_registry = {{"generics", "Generic objects intended for use as the parents of new objects", {$room, $exit, $thing, $note, $letter, $container, $root_class, $player, $prog, $wiz, $generic_editor, $mail_recipient, $mail_agent}}, {"utilities", "Objects holding useful general-purpose verbs", children($generic_utils)}};
  `set_verb_code(this, "user_connected", verb_code(this, "user_connected(core)")) ! ANY';
  `delete_verb(this, "user_connected(core)") ! ANY';
  `set_verb_code(this, "user_reconnected", verb_code(this, "user_reconnected(core)")) ! ANY';
  `delete_verb(this, "user_reconnected(core)") ! ANY';
  for v in ({"do_login_command", "server_started"})
    c = {};
    for i in (verb_code(this, v))
      c = {@c, strsub(i, "$local.login", "$login")};
    endfor
    return c;
    set_verb_code(#0, v, c);
  endfor
endif
.

@verb #0:"user_created user_connected" this none this
@program #0:user_created
if (callers())
  return;
endif
user = args[1];
set_task_perms(user);
try
  user.location:confunc(user);
  ou = $object_utils;
  delay = 1;
  for watcher in ($login.watchers)
    if (ou:has_verb(watcher, verb))
      fork (delay)
        watcher:(verb)(user);
      endfork
    endif
    delay = delay + 1;
  endfor
  "--- used by MCP-aware telnet clients ----";
  $mcp:user_connected(user);
  "----";
  user:confunc();
except id (ANY)
  user:tell("Confunc failed: ", id[2], ".");
  for tb in (id[4])
    user:tell("... called from ", tb[1], ":", tb[2], ", line ", tb[6]);
  endfor
  user:tell("(End of traceback)");
endtry
fork (5)
  $local:announce_player(user);
endfork
.

@verb #0:"user_disconnected user_client_disconnected" this none this
@program #0:user_disconnected
if (callers())
  return;
endif
if (args[1] < #0)
  "not logged in user.  probably should do something clever here involving Carrot's no-spam hack.  --yduJ";
  return;
endif
user = args[1];
user.last_disconnect_time = time();
set_task_perms(user);
`user:disfunc() ! ANY => 0';
ou = $object_utils;
delay = 1;
for watcher in ($login.watchers)
  if (ou:has_verb(watcher, verb))
    fork (1)
      watcher:(verb)(user);
    endfork
  endif
  delay = delay + 1;
endfor
`user.location:disfunc(user) ! ANY => 0';
fork (5)
  $local:denounce_player(user);
endfork
.

@verb #0:"bf_chparent" this none this
@program #0:bf_chparent
"chparent(object, new-parent) -- see help on the builtin. This verb is called by the server when $server_options.protect_chparent exists and is true and caller_perms are not wizardly.";
who = caller_perms();
{what, papa} = args;
if (typeof(what) != OBJ)
  retval = E_TYPE;
elseif (!valid(what))
  retval = E_INVARG;
elseif (typeof(papa) != OBJ)
  retval = E_TYPE;
elseif ((!valid(papa)) && (papa != #-1))
  retval = E_INVIND;
elseif (!$perm_utils:controls(who, what))
  retval = E_PERM;
elseif ((is_player(what) && (!$object_utils:isa(papa, $player_class))) && (!who.wizard))
  retval = E_PERM;
elseif ((is_player(what) && (!$object_utils:isa(what, $player_class))) && (!who.wizard))
  retval = E_PERM;
elseif ((children(what) && $object_utils:isa(what, $player_class)) && (!$object_utils:isa(papa, $player_class)))
  retval = E_PERM;
elseif ((!valid(papa)) || ($perm_utils:controls(who, papa) || papa.f))
  retval = `chparent(@args) ! ANY';
else
  retval = E_PERM;
endif
return ((typeof(retval) == ERR) && $code_utils:dflag_on()) ? raise(retval) | retval;
.

@verb #0:"bf_add_verb" this none this
@program #0:bf_add_verb
"add_verb() -- see help on the builtin for more information. This verb is called by the server when $server_options.protect_add_verb exists and is true and caller_perms() are not wizardly.";
who = caller_perms();
what = args[1];
info = args[2];
if (typeof(what) != OBJ)
  retval = E_TYPE;
elseif (!valid(what))
  retval = E_INVARG;
elseif ((!$perm_utils:controls(who, what)) && (!what.w))
  "caller_perms() is not allowed to hack on the object in question";
  retval = E_PERM;
elseif (!$perm_utils:controls(who, info[1]))
  "caller_perms() is not permitted to add a verb with the specified owner.";
  retval = E_PERM;
elseif (index(info[2], "w") && (!$server_options.permit_writable_verbs))
  retval = E_INVARG;
elseif (!$quota_utils:verb_addition_permitted(who))
  retval = E_QUOTA;
elseif (((what.owner != who) && (!who.wizard)) && (!$quota_utils:verb_addition_permitted(what.owner)))
  retval = E_QUOTA;
elseif (!who.programmer)
  retval = E_PERM;
else
  "we now know that the caller's perms control the object or the object is writable, and we know that the caller's perms control the prospective verb owner (by more traditional means)";
  retval = `add_verb(@args) ! ANY';
endif
return ((typeof(retval) == ERR) && $code_utils:dflag_on()) ? raise(retval) | retval;
.

@verb #0:"bf_add_property" this none this
@program #0:bf_add_property
"add_property() -- see help on the builtin for more information. This verb is called by the server when $server_options.protect_add_property exists and is true and caller_perms() are not wizardly.";
who = caller_perms();
{what, propname, value, info} = args;
if (typeof(what) != OBJ)
  retval = E_TYPE;
elseif (!valid(what))
  retval = E_INVARG;
elseif ((!$perm_utils:controls(who, what)) && (!what.w))
  retval = E_PERM;
elseif (!$perm_utils:controls(who, info[1]))
  retval = E_PERM;
elseif (!$quota_utils:property_addition_permitted(who))
  retval = E_QUOTA;
elseif (((what.owner != who) && (!who.wizard)) && (!$quota_utils:property_addition_permitted(what.owner)))
  retval = E_QUOTA;
  "elseif (!who.programmer)";
  "  return E_PERM;     I wanted to do this, but $builder:@newmessage relies upon nonprogs being able to call add_property.  --Nosredna";
elseif ((propname in {"object_size", "size_quota", "queued_task_limit"}) && (!who.wizard))
  retval = E_PERM;
else
  "we now know that the caller's perms control the object (or the object is writable), and that the caller's perms are permitted to control the new property's owner.";
  retval = `add_property(@args) ! ANY';
endif
return ((typeof(retval) == ERR) && $code_utils:dflag_on()) ? raise(retval) | retval;
.

@verb #0:"bf_recycle" this none this
@program #0:bf_recycle
"recycle(object) -- see help on the builtin. This verb is called by the server when $server_options.protect_recycle exists and is true and caller_perms() are not wizardly.";
if (!valid(what = args[1]))
  retval = E_INVARG;
elseif (!$perm_utils:controls(who = caller_perms(), what))
  retval = E_PERM;
elseif ((p = is_player(what)) && (!who.wizard))
  for p in ($wiz_utils:connected_wizards_unadvertised())
    p:tell($string_utils:pronoun_sub("%N (%#) is currently trying to recycle %t (%[#t])", who, what));
  endfor
  retval = E_PERM;
else
  if (p)
    $wiz_utils:unset_player(what);
  endif
  retval = `recycle(what) ! ANY';
endif
return ((typeof(retval) == ERR) && $code_utils:dflag_on()) ? raise(retval) | retval;
.

@verb #0:"user_reconnected" this none this
@program #0:user_reconnected
if (callers())
  return;
endif
if ($object_utils:isa(user = args[1], $guest))
  "from $guest:boot";
  oldloc = user.location;
  move(user, $nothing);
  "..force enterfunc to be called so that the newbie gets a room description.";
  move(user, user.home);
  user:do_reset();
  if ($object_utils:isa(oldloc, $room))
    oldloc:announce("In the distance you hear someone's alarm clock going off.");
    if (oldloc != user.location)
      oldloc:announce(user.name, " wavers and vanishes into insubstantial mist.");
    else
      oldloc:announce(user.name, " undergoes a wrenching personality shift.");
    endif
  endif
  set_task_perms(user);
  `user:confunc() ! ANY';
endif
set_task_perms(user);
try
  user.location:reconfunc(user);
  ou = $object_utils;
  delay = 1;
  for watcher in ($login.watchers)
    if (ou:has_verb(watcher, verb))
      fork (delay)
        watcher:(verb)(user);
      endfork
    endif
    delay = delay + 1;
  endfor
  "--- used by MCP-aware telnet clients ----";
  $mcp:user_connected(user);
  "----";
  user:reconfunc();
except id (ANY)
  user:tell("Reconfunc failed: ", id[2], ".");
  for tb in (id[4])
    user:tell("... called from ", tb[1], ":", tb[2], ", line ", tb[6]);
  endfor
  user:tell("(End of traceback)");
endtry
.

@verb #0:"bf_set_verb_info" this none this
@program #0:bf_set_verb_info
"set_verb_info() -- see help on the builtin for more information. This verb is called by the server when $server_options.protect_set_verb_info exists and is true and caller_perms() are not wizardly.";
{o, v, i} = args;
if (typeof(vi = `verb_info(o, v) ! ANY') == ERR)
  "probably verb doesn't exist";
  retval = vi;
elseif (!$perm_utils:controls(cp = caller_perms(), vi[1]))
  "perms don't control the current verb owner";
  retval = E_PERM;
elseif ((typeof(i) != LIST) || (typeof(no = i[1]) != OBJ))
  "info is malformed";
  retval = E_TYPE;
elseif ((!valid(no)) || (!is_player(no)))
  "invalid new verb owner";
  retval = E_INVARG;
elseif (!$perm_utils:controls(cp, no))
  "perms don't control prospective verb owner";
  retval = E_PERM;
elseif (index(i[2], "w") && (!`$server_options.permit_writable_verbs ! E_PROPNF, E_INVIND => 1'))
  retval = E_INVARG;
else
  retval = `set_verb_info(o, v, i) ! ANY';
endif
return ((typeof(retval) == ERR) && $code_utils:dflag_on()) ? raise(retval) | retval;
.

@verb #0:"do_out_of_band_command doobc" this none this
@program #0:do_out_of_band_command
"A cheap and very dirty do_out_of_band verb.  Forwards to verb on player with same name.";
if (!callers())
  try
    if (valid(player) && is_player(player))
      set_task_perms(player);
      player:do_out_of_band_command(@args);
    else
      $mcp:do_out_of_band_command(@args);
      $login:do_out_of_band_command(@args);
    endif
  except e (ANY)
    raise(e[1]);
  endtry
endif
.

@verb #0:"do_command" this none this rx
@program #0:do_command
if (callers())
  return E_PERM;
endif
"--- used by MCP-aware telnet clients ---";
if (($mcp:is_reading(player) == 1) && $mcp:process_reading_line(player, argstr))
  return 1;
endif
"---";
return 0;
.

@verb #0:"handle_uncaught_error handle_task_timeout" this none this
@program #0:handle_uncaught_error
valid(caller) && raise(E_PERM);
if (`player.wants_to_see_tracebacks ! ANY' || (`args[4][$][2] ! E_RANGE' == "eval"))
  return 0;
endif
id = tostr(tostr(time())[3..$], random(10) - 1, random(10) - 1);
if (verb == "handle_uncaught_error")
  {time, player, CODE, MSG, VALUE, TRACEBACK, FORMATTED} = $last_tb = {time(), player, @args};
else
  {time, player, CODE, MSG, VALUE, TRACEBACK, FORMATTED} = $last_tb = {time(), player, args[1], "Task ran out of " + args[1], 0, @args[2..3]};
endif
notify(player, tostr("--> Sorry, there was an error while processing your \"", TRACEBACK[$][2], "\" command."));
notify(player, tostr("--> Error id: ", id, "    Message: ", MSG));
fileappend("system", "tracebacks", {id + ":", time, player, toliteral(CODE), MSG, VALUE, toliteral(TRACEBACK), @FORMATTED, ":" + id, ""});
return 1;
.

@verb #0:"announce_guest" this none this rx #177
@program #0:announce_guest
user = args[1];
desc = (typeof(desc = user.description) == LIST) ? $string_utils:from_list(desc, "  ") | desc;
site = $string_utils:connection_hostname(connection_name(user));
#179:receive_page(((((((("(Auto) " + user.name) + " pages, \"") + user.name) + " has connected from site:  ") + site) + ", described as:  ") + desc) + "\"");
#10:receive_page(((((((("(Auto) " + user.name) + " pages, \"") + user.name) + " has connected from site:  ") + site) + ", described as:  ") + desc) + "\"");
#233:receive_page(((((((("(Auto) " + user.name) + " pages, \"") + user.name) + " has connected from site:  ") + site) + ", described as:  ") + desc) + "\"");
"Last modified Mon Jun  2 18:07:52 1997 PDT by Amalgam, #115@GOES.";
.

"***finished***
